{func
   dx_fc_glow_enabler
   ($enabled $player $num_gems_pass $num_gems_miss)
   {if {&& $enabled {== {'+' $num_gems_pass $num_gems_miss} 0}} ;if on fc
      {set_this {find_obj DX_GEMTRACKDIR streak_meter}} ;set context to this player
      {star_deploy.trig trigger} ;color the max mult animation gold
   }
}
{func
   dx_fc_disabler
   ($player $slot $num_gems_pass $num_gems_miss)
   {if {> {'+' $num_gems_pass $num_gems_miss} 0} ;if not on an fc
      {set_this {find_obj DX_GEMTRACKDIR streak_meter}}
      {if {! {eval {var {sprint {$player instrument} "_missed_once"}}}} ;only remove the actual blue backer on first hit
         {set_var {sprint {$player instrument} "_missed_once"} TRUE} ;gate to not run again until restart
         {if $dx_fc_glow
            {end_streak.trig trigger} ;remove blue backer
         }
         {if $dx_fc_ring
            {streak_meter_plate.mat set diffuse_tex multframe.tex} ;remove the FC ring for entire band.
         }
      }
      ;we must remove gold matanim on every hit because OD turns it back on
      {if $dx_fc_glow
         {star_deploy_stop.trig trigger} ;this specifically only removes gold on every hit
      }
   }
}
{func
   dx_ring_fc_enabler
   ($player)
   {set_this {find_obj DX_GEMTRACKDIR streak_meter}}
   {if {! {exists fcframe.tex}}
      {new Tex fcframe.tex} ;generate a new texture to hold the fc indicator
   }
   {if {! {exists multframe.tex}}
      {new Tex multframe.tex} ;generate a new texture to hold the non fc texture
   }
   {fcframe.tex set_bitmap
      {if_else {== $dx_multiplier_ring none}
         {sprint "dx/custom_textures/multiplier_ring/multiplier_ring_plate_fc/rock band 3.png"}
         {do
            {set $temp $dx_multiplier_ring}
            {search_replace $temp "'" "" $temp}
            {sprint "dx/custom_textures/multiplier_ring/multiplier_ring_plate_fc/" $temp ".png"}
         }
      }
   }
   {multframe.tex set_bitmap
      {if_else {== $dx_multiplier_ring none}
         {sprint "dx/custom_textures/multiplier_ring/multiplier_ring_plate/rock band 3.png"}
         {do
            {set $temp $dx_multiplier_ring}
            {search_replace $temp "'" "" $temp}
            {sprint "dx/custom_textures/multiplier_ring/multiplier_ring_plate/" $temp ".png"}
         }
      }
   }
   {if_else $dx_fc_ring
      {streak_meter_plate.mat set diffuse_tex fcframe.tex}
      {streak_meter_plate.mat set diffuse_tex multframe.tex}
   }
}
{func
   dx_track_fc_reset
   ($enabled $player)
   {if $enabled ;on new song to start on fc
      {set_this {find_obj DX_GEMTRACKDIR streak_meter}}
      {peak_state.trig trigger} ;turn on blue backer
      {star_deploy.trig trigger} ;color blue backer gold
   }
}