{set $dx_settings_main ()}
{resize $dx_settings_main 0}
{push_back $dx_settings_main
   (dxState_DeluxeSettings
      (view
         {if_else {$this in_game}
            os_track_speeds
            os_set_speeds
         }
         os_venue_options
         os_track_theme
         ;#ifndef HX_WII
         os_graphics
         ;#endif
         os_audio_sfx
         os_advanced
         #ifdef HX_XBOX
         #ifdef RB3E
         #ifndef RB3E_EMULATOR
         os_reboot
         #endif
         #endif
         #endif
      )
      (enter
         ;-----func----------state name---------message
         ;{dx_state_setup dxState_DeluxeSettings os_welcome_msg}
         ;reapplies the user's own theme when entering dx settings to prevent remote textures from overriding their own theme
         {dx_resync_local_textures all}
      )
      (select
         (os_set_speeds
            {dx_state dxState_SetSpeeds}
         )
         (os_track_speeds
            {dx_state dxState_TrackSpeeds}
         )
         (os_venue_options
            {dx_state dxState_VenueOptions})
         (os_track_theme
            {dx_state #ifdef HX_WII dxState_TextureMenu #else dxState_TrackThemeLoader #endif}
         )
         (os_graphics
            {dx_state dxState_Graphics})
         (os_audio_sfx
            {dx_state dxState_AudioSFX})
         (os_advanced
            {dx_state dxState_Advanced})
         (os_reboot
            {dx_state dxState_RebootWarn})
      )
      (cancel
         {do
            ($options {array 0})
            {leave_dx_settings}
            {if_else $dx_prompt_save
               {dx_state dxState_SaveWarn}
               {if_else $dx_settings_error
                  {dx_state dxState_SaveWarnError}
                  {dx_settings_dta_writer}
                  {$this show_game_options}
               }
            }
         }
      )
   )
}
{push_back $dx_settings_main
   (dxState_Graphics
      (view
         {if_else $dx_60fps os_venuefps_on os_venuefps_off}
         {if_else {== $dx_detected_platform platform_rpcs3}
            {if_else $dx_vsync os_vsync_on os_vsync_off}
            0
         }
         #ifdef HX_PS3
         {if_else $dx_ultra_shadows os_ultra_shadows_on os_ultra_shadows_off}
         #endif
         {if_else $dx_force_lod os_force_high_lod_on os_force_high_lod_off}
         {if_else $dx_postfx os_postfx_on os_postfx_off}
         {if_else $dx_venue_lights os_venue_lights_on os_venue_lights_off}
         {if_else $dx_postfx {if_else $dx_motion_blur os_blur_on os_blur_off} 0}
         {if_else $dx_postfx {if_else $dx_camera_shake os_camera_shake_on os_camera_shake_off} 0}
         {if_else $dx_postfx {if_else $dx_camera_blur os_camera_blur_on os_camera_blur_off} 0}
         {if_else $dx_postfx {if_else $dx_noise os_noise_on os_noise_off} 0}
         os_cycleaspect
      )
      (select
         (os_venuefps
            {set $dx_60fps {! $dx_60fps}}
            {if $dx_60fps
               {set $post_proc_needs_reset TRUE}
            }
            {dx_postfx_setter}
            {if_else $dx_60fps
               {set $emulatefps 60}
               {set $emulatefps 30}
            }
            {if {! $gfx_warn_once}
               {set $gfx_warn_once TRUE}
               {dx_state dxState_GraphicsWarn}
            }
         )
         (os_vsync
            {set $dx_vsync {! $dx_vsync}}
            {rnd set_sync $dx_vsync}
         )
         (os_ultra_shadows
            {set $dx_ultra_shadows {! $dx_ultra_shadows}}
            {if {! $gfx_warn_once}
               {set $gfx_warn_once TRUE}
               {dx_state dxState_GraphicsWarn}
            }
         )
         (os_force_high_lod
            {set $dx_force_lod {! $dx_force_lod}}
            {if $dx_force_lod
               {set $post_proc_needs_reset TRUE}
            }
            {dx_postfx_setter}
            {if {! $lod_warn_once}
               {set $lod_warn_once TRUE}
               {dx_state dxState_HighLODWarn}
            }
         )
         (os_postfx
            {dx_set_postfx {! $dx_postfx}}
            {dx_postfx_setter}
            {if {! $gfx_warn_once}
               {set $gfx_warn_once TRUE}
               {dx_state dxState_GraphicsWarn}
            }
         )
         (os_venue_lights
            {set $dx_venue_lights {! $dx_venue_lights}}
            {if {! $gfx_warn_once}
               {set $gfx_warn_once TRUE}
               {dx_state dxState_GraphicsWarn}
            }
         )
         (os_camera_shake
            {set $dx_camera_shake {! $dx_camera_shake}}
            {if $dx_camera_shake
               {set $post_proc_needs_reset TRUE}
            }
            {dx_postfx_setter}
            {if {! $gfx_warn_once}
               {set $gfx_warn_once TRUE}
               {dx_state dxState_GraphicsWarn}
            }
         )
         (os_camera_blur
            {set $dx_camera_blur {! $dx_camera_blur}}
            {if $dx_camera_blur
               {set $post_proc_needs_reset TRUE}
            }
            {dx_postfx_setter}
            {if {! $gfx_warn_once}
               {set $gfx_warn_once TRUE}
               {dx_state dxState_GraphicsWarn}
            }
         )
         (os_blur
            {set $dx_motion_blur {! $dx_motion_blur}}
            {if $dx_motion_blur
               {set $post_proc_needs_reset TRUE}
            }
            {dx_postfx_setter}
            {if {! $gfx_warn_once}
               {set $gfx_warn_once TRUE}
               {dx_state dxState_GraphicsWarn}
            }
         )
         (os_noise
            {set $dx_noise {! $dx_noise}}
            {if $dx_noise
               {set $post_proc_needs_reset TRUE}
            }
            {dx_postfx_setter}
            {if {! $gfx_warn_once}
               {set $gfx_warn_once TRUE}
               {dx_state dxState_GraphicsWarn}
            }
         )
         (os_cycleaspect
            {dx_cycle_aspect}
         )
      )
      (cancel
         {dx_state dxState_DeluxeSettings}
      )
   )
}
{push_back $dx_settings_main
   (dxState_AudioSFX
      (view
         ;#ifndef HX_WII
         os_fcaudio
         ;#endif
         {if_else $dx_song_select_noise os_audio_ambience_on os_audio_ambience_off}
         {if_else $dx_scroll_sound os_scroll_sound_on os_scroll_sound_off}
         {if_else $dx_whammy_fx os_whammy_fx_on os_whammy_fx_off}
         {if_else $dx_instrument_fx os_audio_instsfx_on os_audio_instsfx_off}
         {switch $dx_selected_guitar_fx
            (0 os_cyclefx_chorus) (1 os_cyclefx_wah) (2 os_cyclefx_flanger)
            (3 os_cyclefx_delay) (4 os_cyclefx_none) (5 os_cyclefx_do_nothing)
         }
         os_toggle_synth
         {if_else $dx_jurgen_sound os_jurgen_sound_on os_jurgen_sound_off}
         {if_else $dx_nice_sound os_nice_sound_on os_nice_sound_off}
      )
      (select
         (os_jurgen_sound {set $dx_jurgen_sound {! $dx_jurgen_sound}})
         (os_audio_ambience {set $dx_song_select_noise {! $dx_song_select_noise}})
         (os_scroll_sound {set $dx_scroll_sound {! $dx_scroll_sound}})
         (os_whammy_fx {set $dx_whammy_fx {! $dx_whammy_fx}})
         (os_audio_instsfx {set $dx_instrument_fx {! $dx_instrument_fx}})
         (os_nice_sound {set $dx_nice_sound {! $dx_nice_sound}})
         #define CYCLE_MSG
         (
            {++ $dx_selected_guitar_fx} ;unrolling the cycle_guitar_fx function to track it separately from the real variable
            {if {== $dx_selected_guitar_fx 6} {set $dx_selected_guitar_fx 0}}
            ;{dx_passive_messenger_symbol {sprintf "Guitar FX set to %s" {elem (wah flanger delay chorus none) $dx_selected_guitar_fx}}}
            {if_else {>= $dx_selected_guitar_fx 5}
               {set $test_guitar_fx 4}
               {set $test_guitar_fx $dx_selected_guitar_fx}
            }
         )
         (os_cyclefx_wah CYCLE_MSG)
         (os_cyclefx_flanger CYCLE_MSG)
         (os_cyclefx_delay CYCLE_MSG)
         (os_cyclefx_chorus CYCLE_MSG)
         (os_cyclefx_none CYCLE_MSG)
         (os_cyclefx_do_nothing CYCLE_MSG)
         (os_toggle_synth
            {toggle_instrument_synth}
            {change_instrument_synth_volume 127}
         )
         (os_fcaudio
            {dx_state dxState_FullComboSFX}
         )
      )
      (cancel
         {dx_state dxState_DeluxeSettings}
      )
   )
}
{push_back $dx_settings_main
   (dxState_FullComboSFX
      (view
         os_none
         os_none
         os_none
         os_none
         os_none
         os_none
         os_none
      )
      (message {sprintf {localize current_fc} {localize $dx_fullcombo_sound}})
      (enter
         {do
            ($list_temp {array 0})
            {push_back $list_temp "goul-nug_fc"}
            {push_back $list_temp os_none}
            {foreach $entry {file_list "sfx/streams/fc/*.mogg"} {if {&& {!= $entry ""} {!= $entry "goul-nug_fc"}}  {push_back $list_temp $entry}}}
            {$current_list set_data $list_temp}
            {choose_char.grp set_showing TRUE}
            {chars.lst set_showing FALSE}
            {chars.sbd set_showing TRUE}
            {chars.sbd set scrollbar_list $current_list}
            ;stolen from dx_state func, manually set selected last picked sound
            {if {find_exists $dx_state_tracker $this_state}
               {do
                  ($data 0)
                  {foreach $entry $dx_state_tracker
                     {if {== {elem $entry 0} $this_state}
                        {set $data {elem $entry 1}}
                     }
                  }
                  {foreach $entry $list_temp
                     {if {has_substr $entry $data}
                        {$current_list set_selected $entry}
                     }
                  }
               }
            }
            {$current_list refresh}
         }
      )
      (select
         (os_none {set $dx_fullcombo_sound dx_none})
         (_fallthrough
            {do
               {set $dx_fullcombo_sound {$component selected_sym}}
               {if_else {exists fc_preview_clip}
                  {do
                     {delete fc_preview_clip}
                     {new MoggClip fc_preview_clip}
                  }
                  {new MoggClip fc_preview_clip}
               }
               {fc_preview_clip set file {sprint "sfx/streams/fc/" $dx_fullcombo_sound ".mogg"}}
               {fc_preview_clip set loop FALSE}
               {fc_preview_clip set volume -8}
               {fc_preview_clip play}
            }
         )
      )
      (cancel
         {dx_state dxState_AudioSFX}
      )
   )
}
{push_back $dx_settings_main
   (dxState_CalibrationInGame
      (view
         {sprintf {localize os_calibrate_video} {int {floor {'+' 0.5 {profile_mgr get_excess_video_lag}}}}}
         {sprintf {localize os_calibrate_audio} {int {floor {'+' 0.5 {profile_mgr get_excess_audio_lag}}}}}
         overshell_calibration
      )
      (enter
         {if {exists beatmatch}
            {beatmatch set_overshell_pause FALSE}
         }
         {$this iterate Mesh $m {$m set_showing FALSE}}
         {$this iterate UIList $l
            {$l set_local_pos_index 0 {+ {$l get_local_pos_index 0} 200}}
            {$l set_local_pos_index 2 {+ {$l get_local_pos_index 2} 250}}
         }
      )
      (button
         #define CALIB_LIST
         (
            {switch {$current_list selected_data}
               (0
                  {set $dx_calibration_mode video}
                  {dx_state dxState_CalibrationInGameSetting}
               )
               (1
                  {set $dx_calibration_mode audio}
                  {dx_state dxState_CalibrationInGameSetting}
               )
               (2 {$this show_enter_calibration}) ;enter vanilla calibration screen
            }
         )
         (kAction_Confirm CALIB_LIST)
      )
      (cancel
         {if {exists beatmatch}
            {beatmatch set_overshell_pause {modifier_mgr is_modifier_active mod_nopause}}
         }
         {$this iterate Mesh $m {$m set_showing TRUE}}
         {$this iterate UIList $l
            {$l set_local_pos_index 0 {- {$l get_local_pos_index 0} 200}}
            {$l set_local_pos_index 2 {- {$l get_local_pos_index 2} 250}}
         }
         {$this show_options_av_settings}
      )
   )
}
{push_back $dx_settings_main
   (dxState_CalibrationInGameSetting
      (view
         {if_else {== $dx_calibration_mode video} {sprintf {localize os_calibrate_video_setting} {int {floor {'+' 0.5 {profile_mgr get_excess_video_lag}}}}} os_blnk}
         {if_else {== $dx_calibration_mode audio} {sprintf {localize os_calibrate_audio_setting} {int {floor {'+' 0.5 {profile_mgr get_excess_audio_lag}}}}} os_blnk}
         os_exit_cal_one ;message telling user to press start to exit
         os_exit_cal_two
      )
      #define CALIB_FORCE_SEL
      (
         {if_else {== $dx_calibration_mode video}
            {$current_list set_selected 0}
            {$current_list set_selected 1}
         }
      )
      (enter
         CALIB_FORCE_SEL
      )
      (select
         (os_exit_cal_one {dx_state dxState_CalibrationInGame})
         (os_exit_cal_two {dx_state dxState_CalibrationInGame})
      )
      (button
         #define CALIB_LEFT
         (
            ;grab current calibration number
            {set $i
               {switch $dx_calibration_mode
                  (video {int {floor {'+' 0.5 {profile_mgr get_excess_video_lag}}}})
                  (audio {int {floor {'+' 0.5 {profile_mgr get_excess_audio_lag}}}})
                  0
               }
            }
            ;add -1 or +1 when pressing left/right dpad
            {set $i
               {switch $action
                  (kAction_Left {- $i 1})
                  (kAction_Right {+ $i 1})
               }
            }
            ;limit calibration from -300 to 300
            {if {> $i 300} {set $i 300}}
            {if {< $i -300} {set $i -300}}
            ;set new calibration number
            {switch $dx_calibration_mode
               (video {profile_mgr set_excess_video_lag $i})
               (audio {profile_mgr set_excess_audio_lag $i})
               kDataUnhandled
            }
            {unless {beatmatch is_invalid_score}
               {beatmatch set_invalid_score TRUE}
               {foreach_int $i 0 4 ;super jank way of setting score to 0 and disabling scoring
                  {cheat_toggle_difficulty}
               }
            }
         )
         (kAction_Left CALIB_LEFT)
         (kAction_Right CALIB_LEFT)
         (kAction_Confirm kDataUnhandled)
         (kAction_Cancel kDataUnhandled)
         (kAction_Up CALIB_FORCE_SEL)
         (kAction_Down CALIB_FORCE_SEL)
         ;pressing start will exit menu to allow user to continue playing with normal frets while calibrating
         (kAction_Start
            {$this iterate Mesh $m {$m set_showing TRUE}}
            {$this iterate UIList $l
               {$l set_local_pos_index 0 {- {$l get_local_pos_index 0} 200}}
               {$l set_local_pos_index 2 {- {$l get_local_pos_index 2} 250}}
            }
            {dx_state dxState_CalibrationInGame}
         )
      )
      (cancel
         {$this iterate Mesh $m {$m set_showing TRUE}}
         {$this iterate UIList $l
            {$l set_local_pos_index 0 {- {$l get_local_pos_index 0} 200}}
            {$l set_local_pos_index 2 {- {$l get_local_pos_index 2} 250}}
         }
         {dx_state dxState_CalibrationInGame}
      )
   )
}
{push_back $dx_settings_main
   (dxState_AngleMenu
      (view
         os_noang
         os_angmult
         {if_else $dx_allow_lower_angle os_allow_lower_ang_on os_allow_lower_ang_off}
      )
      (enter
         {set $dx_slider TRUE}
         {set $dx_slider_id dx_trackangle}
         {instruments.sld set_num_steps 16} ;number of steps for track angle slider
         {instruments.sld set_current {int {/ {- $dx_multiangle 1} {/ 3 15}}}}
         {set $in_slider_state TRUE}
         {if $dx_slider_item_selected
            {$current_list set_showing FALSE}
            {$this set_focus instruments.sld}
         }
      )
      (update_view {handle ($this update_slider instruments.sld "")})
      (select
         (os_noang
            {set $dx_use_multiangle FALSE}
            {set $dx_multiangle 1}
            {instruments.sld set_current {int {/ {- $dx_multiangle 1} {/ 3 15}}}}
         )
         (os_angmult
            {set $dx_slider_item_selected TRUE}
            {set $dx_use_multiangle TRUE}
            {instruments.sld store}
            {instruments.sld set_showing TRUE}
         )
         (os_allow_lower_ang {set $dx_allow_lower_angle {! $dx_allow_lower_angle}})
      )
      (button
         #define ANGLE_SLIDER_ACT
         (
            {{$this find {$this focus_name}} confirm}
            {{$this find {$this focus_name}} set_showing FALSE}
            {$current_list set_showing TRUE}
            {set $dx_slider_item_selected FALSE}
            {$this set_focus $current_list}
            {dx_state dxState_TrackAngleSliderMsg}
         )
         (kAction_Confirm ANGLE_SLIDER_ACT)
         (kAction_Cancel ANGLE_SLIDER_ACT)
      )
      ;(SCROLL_SELECT_MSG {$this on_slider_change $component $user})
      (scroll
         {if {find_elem ("instruments.sld") {$this focus_name}}
            {$this on_slider_change $component $user}
         }
      )
      (cancel
         {set $dx_slider FALSE}
         {set $in_slider_state FALSE}
         {dx_track_angle_var_reset}
         {dx_state dxState_TrackHUD}
      )
   )
}