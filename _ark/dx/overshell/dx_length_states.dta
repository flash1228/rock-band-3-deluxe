   (dxState_TrackLengths
      (view
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 0}} os_blnk os_reset}
         ;{if_else {&& $dx_list_item_selected {!= $dx_list_pos 1}} os_blnk {sprint "interval_" $dx_pos_remapper_array_number}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 2}} os_blnk {sprintf {localize os_trkspd_guitar} {int {+ 0.5 {* $tracklength_guitar 100}}}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 3}} os_blnk {sprintf {localize os_trkspd_bass} {int {+ 0.5 {* $tracklength_bass 100}}}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 4}} os_blnk {sprintf {localize os_trkspd_drum} {int {+ 0.5 {* $tracklength_drum 100}}}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 5}} os_blnk {sprintf {localize os_trkspd_keys} {int {+ 0.5 {* $tracklength_keys 100}}}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 6}} os_blnk {sprintf {localize os_trkspd_real_guitar} {int {+ 0.5 {* $tracklength_real_guitar 100}}}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 7}} os_blnk {sprintf {localize os_trkspd_real_bass} {int {+ 0.5 {* $tracklength_real_bass 100}}}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 8}} os_blnk {sprintf {localize os_trkspd_real_keys} {int {+ 0.5 {* $tracklength_real_keys 100}}}}}
      )
      #define DX_RESET_TRACK_LENGTH
      (
         {foreach $entry (guitar bass drum keys real_guitar real_bass real_keys)
            {set_var {sprint "tracklength_" $entry} 1.0}
         }
      )
      (message
         dx_track_lengths_desc
      )
      (exit
         {set $dx_list_pos 0}
         {set $dx_list_item_selected FALSE}
         {set $dx_pos_remapper_array_number 3}
      )
      (enter
         {set $dx_pos_interval_list (0.01 0.02 0.05 0.10 0.15 0.20 0.25 0.30)}
         {set $dx_pos_current_interval {elem $dx_pos_interval_list $dx_pos_remapper_array_number}} ;1 unit
         {do
            ($arr {find $syscfg dx_settings $this_state view})
            ($data {array 0})
            {foreach $entry $arr
               {if {!= $entry view}
                  {push_back $data $entry}
               }
            }
            {$current_list set_data $data}
         }
      )
      #define LIST_TRACK_LENGTH_DIREC
      (
         {if {!= {$current_list selected_data} 0}
            {switch {$current_list selected_data}
               ;(1 {set $this_var dx_pos_remapper_array_number})
               (1 {set $this_var tracklength_guitar})
               (2 {set $this_var tracklength_bass})
               (3 {set $this_var tracklength_drum})
               (4 {set $this_var tracklength_keys})
               (5 {set $this_var tracklength_real_guitar})
               (6 {set $this_var tracklength_real_bass})
               (7 {set $this_var tracklength_real_keys})
            }
            ;track our current list position for hiding other list elements while we have selected this one
            {set $dx_list_pos {$current_list selected_data}}
            ;actually set our variable, based on the pressed action
            {switch $action
               ((kAction_Left kAction_Down)
                  {if_else {== $this_var dx_pos_remapper_array_number}
                     {if {> {eval {var {sprint $this_var}}} 0}
                        {set_var {sprint $this_var} {- {eval {var $this_var}} 1}}
                     }
                     {dx_modify_tracklength $dx_list_pos {- 0 $dx_pos_current_interval}}
                  }
               )
               ((kAction_Right kAction_Up)
                  {if_else {== $this_var dx_pos_remapper_array_number}
                     {if {< {eval {var {sprint $this_var}}} 7}
                        {set_var {sprint $this_var} {+ {eval {var $this_var}} 1}}
                     }
                     {dx_modify_tracklength $dx_list_pos $dx_pos_current_interval}
                  }
               )
            }
         }
      )
      (button
         (kAction_Confirm
            {if_else $dx_list_item_selected
               {set $dx_list_item_selected FALSE}
               {if_else {== {$current_list selected_data} 0} ;if we click reset
                  {do
                     DX_RESET_TRACK_LENGTH
                  }
                  {do
                     {set $dx_list_item_selected TRUE}
                     {set $dx_list_pos {$current_list selected_data}}
                  }
               }
            }
         )
         (kAction_Cancel
            {if_else $dx_list_item_selected
               {set $dx_list_item_selected FALSE}
               {dx_state dxState_HighwayTheme}
            }
         )
         ;if we pressed Up or Down while inside a selected list item
         (kAction_Down LIST_TRACK_LENGTH_DIREC)
         (kAction_Up LIST_TRACK_LENGTH_DIREC)
         ;if we pressed left or right while hovering on a specific list item
         (kAction_Left LIST_TRACK_LENGTH_DIREC)
         (kAction_Right LIST_TRACK_LENGTH_DIREC)
      )
   )