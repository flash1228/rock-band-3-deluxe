   (dxState_ControllerMenu
      (view
         {if_else 
            {&&
               {== $dx_joypad_type_guitar kControllerGuitar}
               {== $dx_joypad_type_keys kControllerKeys}
               {== $dx_joypad_type_vocals kControllerVocals}
            }
            os_instrumentswap
            os_instreset ;shows controller reset menu if at least one instrument faking is active
         }
         os_pad_mapping
         {if_else {== $dx_joypad_type_vocals kControllerVocals} os_button_remapper 0}
         os_slot_options
      )
      (select
         (os_instrumentswap
            {switch {{$this get_user} get_controller_type}
               (kControllerGuitar
                  {dx_state dxState_InstrumentSwapGuitar})
               (kControllerKeys
                  {dx_state dxState_InstrumentSwapKeys})
               (kControllerVocals
                  {dx_state dxState_InstrumentSwapVocals}
               )
            }
         )
         (os_instreset
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            {leave_dx_settings}
            DX_RESET_INST_TYPES
            ;{dx_state dxState_Advanced} ;force closes the menu if a different player resets the controllers
            {overshell update_all}
         )
         (os_pad_mapping {dx_state dxState_PadMappingSwap})
         (os_button_remapper {dx_state dxState_ButtonRemapper})
         (os_slot_options
            ;{if_else $dx_slot_options_warn_seen
               {dx_state dxState_SlotOptions}
            ;   {dx_state dxState_SlotOptionsWarn}
            ;}
         )
      )
      (cancel {dx_state dxState_DeluxeSettings})
   )

   (dxState_SlotOptions
      (view
         os_reset_everything
         os_allowed_instruments
         {if_else $dx_linear_join_order os_linear_join_order_on os_linear_join_order_off}
      )
      (enter
         ; temp until overshell size is fixed for warn list
         {unless $dx_slot_options_warn_seen
            {dx_passive_messenger os_slot_options_warn}
            {set $dx_slot_options_warn_seen TRUE}
         }
      )
      (select
         (os_reset_everything
            {set $dx_linear_join_order FALSE}

            {set $dx_instruments_slot_1 (guitar real_guitar keys)}
            {set $dx_instruments_slot_2 (drum)}
            {set $dx_instruments_slot_3 (vocals)}
            {set $dx_instruments_slot_4 (guitar real_guitar keys)}

            {set $dx_instruments_aim_slot_1 (guitar real_guitar keys drum vocals)}
            {set $dx_instruments_aim_slot_2 (guitar real_guitar keys drum vocals)}
            {set $dx_instruments_aim_slot_3 (guitar real_guitar keys drum vocals)}
            {set $dx_instruments_aim_slot_4 (guitar real_guitar keys drum vocals)}

            #ifdef RB3E
            {set $dx_slot_options_changed TRUE}
            #else
            {set $dx_slot_options_changed FALSE}
            #endif

            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
         )
         (os_allowed_instruments {dx_state dxState_AllowedInstruments})
         (os_linear_join_order {set $dx_linear_join_order {! $dx_linear_join_order}} {set $dx_slot_options_changed TRUE})
      )
      (cancel
         #ifdef RB3E
         {if_else {&& $dx_slot_options_changed {! $dx_prompt_save}}
            {dx_state dxState_SlotOptionsReboot}
            {dx_state dxState_ControllerMenu}
         }
         #else
         {if {&& $dx_slot_options_changed {! $dx_prompt_save}} {dx_settings_dta_writer}}
         {set $dx_slot_options_changed FALSE}
         {dx_state dxState_ControllerMenu}
         #endif
      )
   )

   (dxState_AllowedInstruments
      (view
         os_allowed_instruments_normal
         mod_auto_vocals
      )
      (select
         (os_allowed_instruments_normal {set $editing_aim_slots FALSE} {dx_state dxState_Slots})
         (mod_auto_vocals {set $editing_aim_slots TRUE} {dx_state dxState_Slots})
      )
      (cancel {dx_state dxState_SlotOptions})
   )

   (dxState_Slots
      (view
         os_slot1
         os_slot2
         os_slot3
         os_slot4
      )
      (select
         (os_slot1 {set $slot_to_edit slot1} {dx_state dxState_AllowedInstrumentsForSlot})
         (os_slot2 {set $slot_to_edit slot2} {dx_state dxState_AllowedInstrumentsForSlot})
         (os_slot3 {set $slot_to_edit slot3} {dx_state dxState_AllowedInstrumentsForSlot})
         (os_slot4 {set $slot_to_edit slot4} {dx_state dxState_AllowedInstrumentsForSlot})
      )
      (cancel {dx_state dxState_AllowedInstruments})
   )


#define DX_APPLY_SLOT_OPTIONS
(
   {cond
      ({== $slot_to_edit slot1}
         {if_else $editing_aim_slots
            {set $dx_instruments_aim_slot_1 $current_slot_array}
            {set $dx_instruments_slot_1 $current_slot_array}
         }
      )
      ({== $slot_to_edit slot2}
         {if_else $editing_aim_slots
            {set $dx_instruments_aim_slot_2 $current_slot_array}
            {set $dx_instruments_slot_2 $current_slot_array}
         }
      )
      ({== $slot_to_edit slot3}
         {if_else $editing_aim_slots
            {set $dx_instruments_aim_slot_3 $current_slot_array}
            {set $dx_instruments_slot_3 $current_slot_array}
         }
      )
      ({== $slot_to_edit slot4}
         {if_else $editing_aim_slots
            {set $dx_instruments_aim_slot_4 $current_slot_array}
            {set $dx_instruments_slot_4 $current_slot_array}
         }
      )
   }
   {set $dx_slot_options_changed TRUE}
   {dx_state dxState_Slots}
)

   (dxState_AllowedInstrumentsForSlot
      (view
         os_allow_all
         os_strings_keys_only
         os_all_but_drums
         os_drums_only
         os_all_but_vocals
         os_vocals_only
         os_all_but_keys
         os_keys_only
      )
      (select
         (os_allow_all {set $current_slot_array (guitar real_guitar keys drum vocals)} DX_APPLY_SLOT_OPTIONS)
         (os_strings_keys_only {set $current_slot_array (guitar real_guitar keys)} DX_APPLY_SLOT_OPTIONS)
         (os_all_but_drums {set $current_slot_array (guitar real_guitar keys vocals)} DX_APPLY_SLOT_OPTIONS)
         (os_drums_only {set $current_slot_array (drum)} DX_APPLY_SLOT_OPTIONS)
         (os_all_but_vocals {set $current_slot_array (guitar real_guitar keys drum)} DX_APPLY_SLOT_OPTIONS)
         (os_vocals_only {set $current_slot_array (vocals)} DX_APPLY_SLOT_OPTIONS)
         (os_all_but_keys {set $current_slot_array (guitar real_guitar drum vocals)} DX_APPLY_SLOT_OPTIONS)
         (os_keys_only {set $current_slot_array (keys)} DX_APPLY_SLOT_OPTIONS)
      )
      (cancel {dx_state dxState_Slots})
   )

   (dxState_PadMappingSwap
      (view
         os_pad
         os_rbguitar
         os_ghguitar
         os_rbdrums
         os_rb2drums
         os_ghdrums
      )
      (message os_pad_mapping_select_desc)
      (select
         (os_pad {set $dx_joypad_pad_mapping joypad})
         (os_rbguitar {set $dx_joypad_pad_mapping #ifdef HX_PS3 hx_guitar_ps3 #else #ifdef HX_WII wii_guitar #else ro_guitar_xbox #endif #endif})
         (os_ghguitar {set $dx_joypad_pad_mapping #ifdef HX_PS3 ro_guitar_ps3 #else #ifdef HX_WII wii_guitar #else ro_guitar_xbox #endif #endif})
         (os_rbdrums {set $dx_joypad_pad_mapping #ifdef HX_PS3 hx_drums_ps3 #else #ifdef HX_WII wii_drums #else hx_drums_xbox #endif #endif})
         (os_rb2drums {set $dx_joypad_pad_mapping #ifdef HX_PS3 hx_drums_ps3 #else #ifdef HX_WII wii_drums #else hx_drums_xbox_rb2 #endif #endif})
         (os_ghdrums {set $dx_joypad_pad_mapping #ifdef HX_PS3 ro_drums_ps3 #else #ifdef HX_WII wii_rodrums #else ro_drums_xbox #endif #endif})
      )
      (cancel {dx_state dxState_ControllerMenu})
   )

   (dxState_InstrumentSwapGuitar
      (view keys drums vocals)
      (message os_instrumentswap_menu_guitar_desc)
      (select
         (keys
            {leave_dx_settings}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_guitar kControllerKeys}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_GUITAR
            {dx_log_writer info {sprint "Guitar instrument type has been changed to: " {find $syscfg joypad instrument_mapping ro_guitar}}}
         )
         (drums
            {leave_dx_settings}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_guitar kControllerDrum}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_GUITAR
            {dx_log_writer info {sprint "Guitar instrument type has been changed to: " {find $syscfg joypad instrument_mapping ro_guitar}}}
         )
         (vocals
            {leave_dx_settings}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_guitar kControllerVocals}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_GUITAR
            {dx_log_writer info {sprint "Guitar instrument type has been changed to: " {find $syscfg joypad instrument_mapping ro_guitar}}}
         )
      )
      (cancel {dx_state dxState_ControllerMenu})
   )

   (dxState_InstrumentSwapKeys
      (view guitar drums vocals)
      (message os_instrumentswap_menu_keys_desc)
      (select
         (guitar
            {leave_dx_settings}
            {$this hide_message}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_keys kControllerGuitar}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_KEYS
            {dx_log_writer info {sprint "Keys instrument type has been changed to: " {find $syscfg joypad instrument_mapping hx_midi_keys_xbox}}}
         )
         (drums
            {leave_dx_settings}
            {$this hide_message}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_keys kControllerDrum}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_KEYS
            {dx_log_writer info {sprint "Keys instrument type has been changed to: " {find $syscfg joypad instrument_mapping hx_midi_keys_xbox}}}
         )
         (vocals
            {leave_dx_settings}
            {$this hide_message}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_keys kControllerVocals}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_KEYS
            {dx_log_writer info {sprint "Keys instrument type has been changed to: " {find $syscfg joypad instrument_mapping hx_midi_keys_xbox}}}
         )
      )
      (cancel {dx_state dxState_ControllerMenu})
   )

   (dxState_InstrumentSwapVocals
      (view guitar keys drums)
      (message os_instrumentswap_menu_vocals_desc)
      (select
         (guitar
            {leave_dx_settings}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_vocals kControllerGuitar}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_VOCALS
            {dx_log_writer info {sprint "Vocals instrument type has been changed to: " {find $syscfg joypad instrument_mapping analog}}}
         )
         (drums
            {leave_dx_settings}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_vocals kControllerDrum}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_VOCALS
            {dx_log_writer info {sprint "Vocals instrument type has been changed to: " {find $syscfg joypad instrument_mapping analog}}}
         )
         (keys
            {leave_dx_settings}
            DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
            {set $dx_joypad_type_vocals kControllerKeys}
            {if {! $dx_prompt_save} {dx_settings_dta_writer}}
            DX_SWAP_INSTRUMENT_TYPE_VOCALS
            {dx_log_writer info {sprint "Vocals instrument type has been changed to: " {find $syscfg joypad instrument_mapping analog}}}
         )
      )
      (cancel {dx_state dxState_ControllerMenu})
   )

;#define kPad_L2
;(0)
;#define kPad_R2
;(1)
;#define kPad_L1
;(2)
;#define kPad_R1
;(3)
;#define kPad_Tri
;(4)
;#define kPad_Circle
;(5)
;#define kPad_X
;(6)
;#define kPad_Square
;(7)
;#define kPad_Select
;(8)
;#define kPad_L3
;(9)
;#define kPad_R3
;(10)
;#define kPad_Start
;(11)
;#define kPad_DUp
;(12)
;#define kPad_DRight
;(13)
;#define kPad_DDown
;(14)
;#define kPad_DLeft
;(15)
;{set_elem {find $syscfg beatmatcher controller joypad slots} 1 kPad_DLeft}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 2 0}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 3 kPad_DUp}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 4 1}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 5 kPad_Square}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 6 2}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 7 kPad_Tri}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 8 3}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 9 kPad_Circle}
;{set_elem {find $syscfg beatmatcher controller joypad slots} 10 4}
   (dxState_ButtonRemapper
      (view
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 0}} os_blnk os_reset}
         ;{if_else {&& $dx_list_item_selected {!= $dx_list_pos 1}} os_blnk {sprint "interval_" $dx_pos_remapper_array_number}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 1}} os_blnk {sprint "mapped_button_" {elem $dx_button_list $dx_button_0_array_num}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 2}} os_blnk {sprint "mapped_button_" {elem $dx_button_list $dx_button_1_array_num}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 3}} os_blnk {sprint "mapped_button_" {elem $dx_button_list $dx_button_2_array_num}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 4}} os_blnk {sprint "mapped_button_" {elem $dx_button_list $dx_button_3_array_num}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 5}} os_blnk {sprint "mapped_button_" {elem $dx_button_list $dx_button_4_array_num}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 6}} os_blnk {sprint "mapped_button_" {elem $dx_button_list $dx_button_5_array_num}}}
      )
      #define DX_RESET_BUTTON_REMAP
      (
         {dx_passive_messenger_symbol {sprint "implement reset"}}
      )
      (message os_button_remapper_desc)
      (exit
         {set $dx_list_pos 0}
         {set $dx_list_item_selected FALSE}
         {set $dx_pos_remapper_array_number 3}
      )
      (enter
         {do
            ($arr {find $syscfg dx_settings $this_state view})
            ($data {array 0})
            {foreach $entry $arr
               {if {!= $entry view}
                  {push_back $data $entry}
               }
            }
            {$current_list set_data $data}
         }
      )
      #define LIST_BUTTON_REMAP_DIREC
      (
         {if {!= {$current_list selected_data} 0}
            {switch {$current_list selected_data}
               ;(1 {set $this_var dx_pos_remapper_array_number})
               (1
                  {set $this_var dx_button_0_array_num}
                  {set $this_elem 1}
                  {set $this_item slots}
               )
               (2
                  {set $this_var dx_button_1_array_num}
                  {set $this_elem 3}
                  {set $this_item slots}
               )
               (3
                  {set $this_var dx_button_2_array_num}
                  {set $this_elem 5}
                  {set $this_item slots}
               )
               (4
                  {set $this_var dx_button_3_array_num}
                  {set $this_elem 7}
                  {set $this_item slots}
               )
               (5
                  {set $this_var dx_button_4_array_num}
                  {set $this_elem 9}
                  {set $this_item slots}
               )
               (6
                  {set $this_var dx_button_5_array_num}
                  {set $this_elem 1}
                  {set $this_item force_mercury}
               )
            }
            ;track our current list position for hiding other list elements while we have selected this one
            {set $dx_list_pos {$current_list selected_data}}
            ;actually set our variable, based on the pressed action
            {switch $action
               ((kAction_Left kAction_Down)
                  {if {> {eval {var {sprint $this_var}}} 0}
                     {set_var {sprint $this_var} {- {eval {var $this_var}} 1}}
                     {set_elem {find $syscfg beatmatcher controller joypad $this_item} $this_elem {elem $dx_button_list {eval {var $this_var}}}}
                  }
               )
               ((kAction_Right kAction_Up)
                  {if {< {eval {var {sprint $this_var}}} {- {size $dx_button_list} 1}}
                     {set_var {sprint $this_var} {+ {eval {var $this_var}} 1}}
                     {set_elem {find $syscfg beatmatcher controller joypad $this_item} $this_elem {elem $dx_button_list {eval {var $this_var}}}}
                  }
               )
            }
         }
      )
      (button
         (kAction_Confirm
            {if_else $dx_list_item_selected
               {set $dx_list_item_selected FALSE}
               {if_else {== {$current_list selected_data} 0} ;if we click reset
                  {do
                     DX_RESET_BUTTON_REMAP
                  }
                  {do
                     {set $dx_list_item_selected TRUE}
                     {set $dx_list_pos {$current_list selected_data}}
                  }
               }
            }
         )
         (kAction_Cancel
            {if_else $dx_list_item_selected
               {set $dx_list_item_selected FALSE}
               {do
                  {if {! $dx_prompt_save} {dx_settings_dta_writer}}
                  {dx_state dxState_ControllerMenu}
               }
            }
         )
         ;if we pressed Up or Down while inside a selected list item
         (kAction_Down LIST_BUTTON_REMAP_DIREC)
         (kAction_Up LIST_BUTTON_REMAP_DIREC)
         ;if we pressed left or right while hovering on a specific list item
         (kAction_Left LIST_BUTTON_REMAP_DIREC)
         (kAction_Right LIST_BUTTON_REMAP_DIREC)
      )
   )