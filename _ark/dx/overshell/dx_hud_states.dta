   (dxState_InGameFeatures
      (view
         os_lyric_display
         os_online
         os_bandinfo
         os_audio_sfx
      )
      (select
         (os_lyric_display {dx_state dxState_LyricDisplay})
         (os_bandinfo {dx_state dxState_BandInfo})
         (os_online {dx_state dxState_OnlineFeatures})
         (os_audio_sfx {dx_state dxState_AudioSFX})
      )
      (cancel {dx_state dxState_DeluxeSettings})
   )

   (dxState_LyricDisplay
      (view
         {if_else $dx_lyric_display_enabled os_lyric_display_on os_lyric_display_off}
         os_lyric_highlight_color
      )
      (select
         (os_lyric_display {set $dx_lyric_display_enabled {! $dx_lyric_display_enabled}})
         (os_lyric_highlight_color
            {set $dx_set_ui_rgb_tracked_ui lyric_highlight}
            {dx_state dxState_rgbcolormenu}
         )
      )
      (cancel {dx_state dxState_InGameFeatures})
   )

   (dxState_BandInfo
      (view
         ;crowd meter
         {if_else {== $dx_crowd_meter_hud dx_on} os_vis_default 0}
         {if_else {== $dx_crowd_meter_hud dx_off} os_vis_static 0}
         {if_else {== $dx_crowd_meter_hud dx_disabled} os_vis_disabled 0}
         {if_else $dx_fail_flash os_fail_flash_on os_fail_flash_off}
         ;stars
         {if_else $dx_gold_star_progress os_star_gold_fill_on os_star_gold_fill_off}
      )
      (select
         #define CROWD_METER_SELECT
         (
            {do
               {set $dx_crowd_meter_hud
                  {switch $dx_crowd_meter_hud
                     (dx_on dx_off)
                     (dx_off dx_disabled)
                     (dx_disabled dx_on)
                  }
               }
               {dx_toggle_fail_feedback}
            }
         )
         (os_vis_static CROWD_METER_SELECT)
         (os_vis_default CROWD_METER_SELECT)
         (os_vis_disabled CROWD_METER_SELECT)
         (os_fail_flash {set $dx_fail_flash {! $dx_fail_flash}})
         (os_star_gold_fill {set $dx_gold_star_progress {! $dx_gold_star_progress}})
      )
      (cancel {dx_state dxState_InGameFeatures})
   )

   (dxState_InGameHUD
      (view
         ;solo box position
         ;os_scoreboard_pos
         os_star_display_pos
         os_solo_box_pos
         {if_else {gamemode in_mode practice} os_practice_hud 0}
      )
      (select
         ;(os_scoreboard_pos
         ;   {set $dx_set_ui_pos_tracked_ui scoreboard}
         ;   {dx_state dxState_PositionRemapper})
         (os_star_display_pos
            {set $dx_set_ui_pos_tracked_ui star_display}
            {dx_state dxState_PositionRemapper}
         )
         (os_solo_box_pos
            {set $dx_set_ui_pos_tracked_ui solo_box}
            {dx_state dxState_PositionRemapper}
         )
         (os_practice_hud {dx_state dxState_ScreenElements_Practice})
      )
      (cancel {dx_state dxState_HudTheme})
   )

   (dxState_OnlineFeatures
      (view
         {if_else $dx_dim_remote_players os_dim_remote_players_on os_dim_remote_players_off}
         {if_else $dx_force_remote_vox os_force_remote_vox_on os_force_remote_vox_off}
         os_quick_chat
      )
      (select
         (os_dim_remote_players {set $dx_dim_remote_players {! $dx_dim_remote_players}})
         (os_force_remote_vox
            {set $dx_force_remote_vox {! $dx_force_remote_vox}}
            {if {&& {$this in_game} $dx_active_vocals} {coop_track_panel dx_force_remote_vox}}
         )
         (os_quick_chat {dx_state dxState_ChatSettings})
      )
      (cancel {dx_state dxState_InGameFeatures})
   )

   (dxState_TrackPopups
      (view
         {if_else $dx_note_streak_popups os_note_streak_on os_note_streak_off}
         {if_else $dx_od_ready_popups os_od_ready_on os_od_ready_off}
         {if_else $dx_high_score_report os_high_score_on os_high_score_off}
         {if_else {== $dx_hide_on_countdown single} os_hide_with_countdown_single 0}
         {if_else {== $dx_hide_on_countdown multi} os_hide_with_countdown_multi 0}
         {if_else {== $dx_hide_on_countdown off} os_hide_with_countdown_disable 0}
         {if_else $dx_end_song_feedback os_end_song_feedback_on os_end_song_feedback_off}
         {if_else $dx_you_rock os_you_rock_on os_you_rock_off}
      )
      (select
         (os_high_score {set $dx_high_score_report {! $dx_high_score_report}})
         (os_note_streak {set $dx_note_streak_popups {! $dx_note_streak_popups}})
         (os_od_ready {set $dx_od_ready_popups {! $dx_od_ready_popups}})
         (os_end_song_feedback {set $dx_end_song_feedback {! $dx_end_song_feedback}})
         (os_you_rock {set $dx_you_rock {! $dx_you_rock}})
         (os_hide_with_countdown_disable {set $dx_hide_on_countdown single})
         (os_hide_with_countdown_multi {set $dx_hide_on_countdown off})
         (os_hide_with_countdown_single {set $dx_hide_on_countdown multi})
      )
      (cancel {dx_state dxState_HighwayPlayerStats})
   )

   (dxState_MTVElements
      (view
         {if_else {== $dx_mtv_visibility always} os_mtv_vis_always 0}
         {if_else {== $dx_mtv_visibility never} os_mtv_vis_never 0}
         {if_else {== $dx_mtv_visibility delayed} os_mtv_vis_delayed 0}
         {if_else {== $dx_mtv_visibility default} os_mtv_vis_default 0}
         os_pos
         os_currentsong
         os_text_options
      )
      (select
         (os_currentsong {dx_state dxState_CurrentSongSetup})
         (os_text_options
            {set $dx_set_ui_label_tracked_ui mtv_overlay}
            {dx_state dxState_LabelOptions}
         )
         (os_pos
            {set $dx_set_ui_pos_tracked_ui mtv}
            {dx_state dxState_PositionRemapper}
         )
         #define MTV_TRIGGER
         (
            {if {$this in_game}
               {if
                  {&&
                     {! {gamemode in_mode practice}} {! {gamemode in_mode trainer}}
                  }
                  {if $dx_mtvup
                     {{{gamemode get track_panel} find mtv_overlay} intro_end}
                  }
               }
            }
         )
         (os_mtv_vis_default
            {set $dx_mtv_visibility always}
            {set $dx_long_mtv_delay FALSE}
            MTV_TRIGGER
         )
         (os_mtv_vis_always
            {set $dx_mtv_visibility never}
            {set $dx_long_mtv_delay FALSE}
            MTV_TRIGGER
         )
         (os_mtv_vis_never
            {set $dx_mtv_visibility delayed}
            {set $dx_long_mtv_delay TRUE}
            MTV_TRIGGER
         )
         (os_mtv_vis_delayed
            {set $dx_mtv_visibility default}
            {set $dx_long_mtv_delay FALSE}
            MTV_TRIGGER
         )
      )
      (cancel {dx_state dxState_HudTheme})
   )

   (dxState_CurrentSongSetup
      (view
         {if_else $dx_mtv_line_artist os_artist_on os_artist_off}
         {if_else $dx_mtv_year os_year_on os_year_off}
         {if_else $dx_mtv_line_album os_album_on os_album_off}
         {if_else $dx_mtv_line_genre os_genre_on os_genre_off}
         {if_else $dx_mtv_line_sub_genre os_sub_genre_on os_sub_genre_off}
         {if_else $dx_mtv_line_game_origin os_source_on os_source_off}
         {if_else $dx_mtv_line_author os_author_on os_author_off}
         {if_else $dx_mtv_line_song_key os_song_key_on os_song_key_off}
         {if_else $dx_mtv_line_section os_section_on os_section_off}
         {if_else $dx_mtv_line_bpm os_bpm_on os_bpm_off}
      )
      (message os_mtv_message_desc)
      (select
         #define MTV_REFRESH
         (
            {set $mtv_destroy TRUE}
            {if $dx_mtvup
               {{{gamemode get track_panel} find mtv_overlay} mtv_formatter} ;refresh the mtv overlay
            }
            {set $mtv_destroy FALSE}
         )
         (os_prefix {set $dx_currentsong_prefix {! $dx_currentsong_prefix}} MTV_REFRESH)
         (os_artist {set $dx_mtv_line_artist {! $dx_mtv_line_artist}} MTV_REFRESH)
         (os_year {set $dx_mtv_year {! $dx_mtv_year}} MTV_REFRESH)
         (os_yearonline {set $dx_year_on_one_mtv_line {! $dx_year_on_one_mtv_line}} MTV_REFRESH)
         (os_album {set $dx_mtv_line_album {! $dx_mtv_line_album}} MTV_REFRESH)
         (os_section {set $dx_mtv_line_section {! $dx_mtv_line_section}} MTV_REFRESH)
         (os_song_title {set $dx_mtv_line_song_title {! $dx_mtv_line_song_title}} MTV_REFRESH)
         (os_genre {set $dx_mtv_line_genre {! $dx_mtv_line_genre}} MTV_REFRESH)
         (os_sub_genre {set $dx_mtv_line_sub_genre {! $dx_mtv_line_sub_genre}} MTV_REFRESH)
         (os_source {set $dx_mtv_line_game_origin {! $dx_mtv_line_game_origin}} MTV_REFRESH)
         (os_author {set $dx_mtv_line_author {! $dx_mtv_line_author}} MTV_REFRESH)
         (os_bpm {set $dx_mtv_line_bpm {! $dx_mtv_line_bpm}} MTV_REFRESH)
         (os_song_key {set $dx_mtv_line_song_key {! $dx_mtv_line_song_key}} MTV_REFRESH)
      )
      (cancel {dx_state dxState_MTVElements})
   )

   (dxState_ScreenElements_Practice
      (view
         os_all_item
         os_speed_control
         os_score_percent
         os_section
      )
      (select
         (os_all_item
            {set $dx_set_ui_pos_tracked_ui all_item}
            {dx_state dxState_PositionRemapper}
         )
         (os_speed_control
            {set $dx_set_ui_pos_tracked_ui speed_control}
            {dx_state dxState_PositionRemapper}
         )
         (os_score_percent
            {set $dx_set_ui_pos_tracked_ui score}
            {dx_state dxState_PositionRemapper}
         )
         (os_section
            {set $dx_set_ui_pos_tracked_ui section}
            {dx_state dxState_PositionRemapper}
         )
      )
      (cancel {dx_state dxState_InGameHUD})
   )

   (dxState_TrackHighwayText
      (view
         os_highway_username
         os_highway_streak
         os_highway_overdrive
         os_highway_perfects
      )
      (select
         #define OS_SETUP_TRACK_LABEL
         (
            {if {$this in_game}
               {beatmatch foreach_active_player $player
                  {if {!= {$player instrument} vocals}
                     {dx_track_label dx_track_label_username $dx_track_username_size $dx_track_username_font $dx_track_username_alignment $dx_track_username_kerning {{$player get_user} get_slot_num} $dx_track_username_x $dx_track_username_y $dx_track_username_r $dx_track_username_g $dx_track_username_b TRUE}
                     {dx_show_track_username $dx_track_username $player}
                     {dx_track_label_overdrive dx_track_label_overdrive $dx_track_overdrive_size $dx_track_overdrive_font $dx_track_overdrive_alignment $dx_track_overdrive_kerning {{$player get_user} get_slot_num} $dx_track_overdrive_x $dx_track_overdrive_y $dx_track_overdrive_r $dx_track_overdrive_g $dx_track_overdrive_b TRUE}
                     {dx_track_label_streak dx_track_label_streak $dx_track_streak_size $dx_track_streak_font $dx_track_streak_alignment $dx_track_streak_kerning {{$player get_user} get_slot_num} $dx_track_streak_x $dx_track_streak_y $dx_track_streak_r $dx_track_streak_g $dx_track_streak_b TRUE}
                     {dx_track_label_streak dx_track_label_perfect $dx_track_perfect_size $dx_track_perfect_font $dx_track_perfect_alignment $dx_track_perfect_kerning {{$player get_user} get_slot_num} $dx_track_perfect_x $dx_track_perfect_y $dx_track_perfect_r $dx_track_perfect_g $dx_track_perfect_b TRUE}
                     {dx_track_label_streak dx_track_label_perfect_ctr $dx_track_perfect_ctr_size $dx_track_perfect_ctr_font $dx_track_perfect_ctr_alignment $dx_track_perfect_ctr_kerning {{$player get_user} get_slot_num} $dx_track_perfect_ctr_x $dx_track_perfect_ctr_y $dx_gold_ui_r $dx_gold_ui_g $dx_gold_ui_b TRUE}
                  }
               }
            }
         )
         (os_highway_streak {dx_state dxState_TrackHighwayStreak})
         (os_highway_username {dx_state dxState_TrackHighwayUsername})
         (os_highway_overdrive {dx_state dxState_TrackHighwayOverdrive})
         (os_highway_perfects {dx_state dxState_TrackPerfects})
      )
      (cancel {dx_state dxState_HighwayPlayerStats})
   )

   (dxState_TrackPerfects
      (view
         {if_else $dx_perfects_indicator os_perfects_indicator_on os_perfects_indicator_off}
         {if_else $dx_perfects_ctr os_perfects_ctr_on os_perfects_ctr_off}
         {if_else $dx_detailed_hit_stats os_detailed_hit_on os_detailed_hit_off}
         {if_else $dx_detailed_hit_timing os_hit_timing_on os_hit_timing_off}
      )
      (select
         (os_perfects_indicator
            {if_else {&& {! $dx_perfects_indicator} {$this in_game}}
               {dx_state dxState_JudgmentWarn}
               {set $dx_perfects_indicator {! $dx_perfects_indicator}}
            }
         )
         (os_perfects_ctr {set $dx_perfects_ctr {! $dx_perfects_ctr}})
         (os_detailed_hit {set $dx_detailed_hit_stats {! $dx_detailed_hit_stats}})
         (os_hit_timing {set $dx_detailed_hit_timing {! $dx_detailed_hit_timing}})
      )
      (cancel {dx_state dxState_InGameFeatures})
   )

   (dxState_TrackHighwayUsername
      (view
         {if_else $dx_track_username os_highway_username_on os_highway_username_off}
         {if_else $dx_highway_username_inst os_highway_username_inst_on os_highway_username_inst_off}
         {if_else $dx_highway_username_sp_inst os_highway_username_sp_inst_on os_highway_username_sp_inst_off}
         os_pos
         os_col
         os_text_options
      )
      (select
         #define OS_HIGHWAY_USER_SETUP
         (
            {beatmatch foreach_active_player $player
               {if {!= {$player instrument} vocals}
                  {dx_track_label dx_track_label_username $dx_track_username_size $dx_track_username_font $dx_track_username_alignment $dx_track_username_kerning {{$player get_user} get_slot_num} $dx_track_username_x $dx_track_username_y $dx_track_username_r $dx_track_username_g $dx_track_username_b TRUE}
                  {dx_show_track_username $dx_track_username $player}
               }
            }
         )
         (os_highway_username
            {set $dx_track_username {! $dx_track_username}}
            OS_HIGHWAY_USER_SETUP
         )
         (os_highway_username_inst
            {set $dx_highway_username_inst {! $dx_highway_username_inst}}
            OS_HIGHWAY_USER_SETUP
         )
         (os_highway_username_sp_inst
            {set $dx_highway_username_sp_inst {! $dx_highway_username_sp_inst}}
            OS_HIGHWAY_USER_SETUP
         )
         (os_pos
            {set $dx_set_ui_pos_tracked_ui highway_username_text}
            {dx_state dxState_PositionRemapper}
         )
         (os_col
            {set $dx_set_ui_rgb_tracked_ui highway_username_text}
            {dx_state dxState_rgbcolormenu}
         )
         (os_text_options
            {set $dx_set_ui_label_tracked_ui highway_username_text}
            {dx_state dxState_LabelOptions}
         )
      )
      (cancel {dx_state dxState_TrackHighwayText})
   )

   (dxState_TrackHighwayOverdrive
      (view
         {if_else $dx_track_overdrive os_highway_overdrive_on os_highway_overdrive_off}
         os_pos
         os_col
         os_text_options
      )
      (select
         (os_highway_overdrive
            {set $dx_track_overdrive {! $dx_track_overdrive}}
            {if {$this in_game}
               {beatmatch foreach_active_player $player
                  {if {!= {$player instrument} vocals}
                     {dx_track_label_overdrive dx_track_label_overdrive $dx_track_overdrive_size $dx_track_overdrive_font $dx_track_overdrive_alignment $dx_track_overdrive_kerning {{$player get_user} get_slot_num} $dx_track_overdrive_x $dx_track_overdrive_y $dx_track_overdrive_r $dx_track_overdrive_g $dx_track_overdrive_b TRUE}
                  }
               }
            }
         )
         (os_pos
            {set $dx_set_ui_pos_tracked_ui highway_overdrive_text}
            {dx_state dxState_PositionRemapper}
         )
         (os_col
            {set $dx_set_ui_rgb_tracked_ui highway_overdrive_text}
            {dx_state dxState_rgbcolormenu}
         )
         (os_text_options
            {set $dx_set_ui_label_tracked_ui highway_overdrive_text}
            {dx_state dxState_LabelOptions}
         )
      )
      (cancel {dx_state dxState_TrackHighwayText})
   )

   (dxState_TrackHighwayStreak
      (view
         {if_else $dx_track_streak os_highway_streak_on os_highway_streak_off}
         {if_else $dx_track_streak_gh3_delay os_highway_streak_gh3_delay_on os_highway_streak_gh3_delay_off}
         os_pos
         os_col
         os_text_options
      )
      (select
         #define OS_SET_LABEL_STREAK
         (
            {if {$this in_game}
               {beatmatch foreach_active_player $player
                  {if {!= {$player instrument} vocals}
                     {dx_track_label_streak dx_track_label_streak $dx_track_streak_size $dx_track_streak_font $dx_track_streak_alignment $dx_track_streak_kerning {{$player get_user} get_slot_num} $dx_track_streak_x $dx_track_streak_y $dx_track_streak_r $dx_track_streak_g $dx_track_streak_b TRUE}
                  }
               }
            }
         )
         (os_highway_streak
            {set $dx_track_streak {! $dx_track_streak}}
            OS_SET_LABEL_STREAK
         )
         (os_highway_streak_gh3_delay
            {set $dx_track_streak_gh3_delay {! $dx_track_streak_gh3_delay}}
            OS_SET_LABEL_STREAK
         )
         (os_pos
            {set $dx_set_ui_pos_tracked_ui highway_streak_text}
            {dx_state dxState_PositionRemapper}
         )
         (os_col
            {set $dx_set_ui_rgb_tracked_ui highway_streak_text}
            {dx_state dxState_rgbcolormenu}
         )
         (os_text_options
            {set $dx_set_ui_label_tracked_ui highway_streak_text}
            {dx_state dxState_LabelOptions}
         )
      )
      (cancel {dx_state dxState_TrackHighwayText})
   )

   (dxState_LabelOptions
      (view
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 0}} os_blnk os_reset}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 1}} os_blnk {sprint "interval_" $dx_pos_remapper_array_number}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 2}} os_blnk {sprint {localize os_font_size} ": " $dx_set_ui_text_size}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 3}} os_blnk {sprint {localize os_kerning}  ": " $dx_set_ui_text_kerning}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 4}} os_blnk {sprint {localize os_alignment}  ": " $dx_set_ui_text_alignment}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 5}} os_blnk {sprint os_font $dx_set_ui_text_font}}
      )
      (message pos_remap_desc)
      #define DX_TRANSLATE_LABEL_VARIABLES_TO_LOCAL
      (
         {switch $dx_set_ui_label_tracked_ui
            (highway_overdrive_text
               {set $dx_set_ui_text_size $dx_track_overdrive_size}
               {set $dx_set_ui_text_font $dx_track_overdrive_font}
               {set $dx_set_ui_text_alignment $dx_track_overdrive_alignment}
               {set $dx_set_ui_text_kerning $dx_track_overdrive_kerning}
            )
            (highway_streak_text
               {set $dx_set_ui_text_size $dx_track_streak_size}
               {set $dx_set_ui_text_font $dx_track_streak_font}
               {set $dx_set_ui_text_alignment $dx_track_streak_alignment}
               {set $dx_set_ui_text_kerning $dx_track_streak_kerning}
            )
            (highway_username_text
               {set $dx_set_ui_text_size $dx_track_username_size}
               {set $dx_set_ui_text_font $dx_track_username_font}
               {set $dx_set_ui_text_alignment $dx_track_username_alignment}
               {set $dx_set_ui_text_kerning $dx_track_username_kerning}
            )
            (mtv_overlay
               {set $dx_set_ui_text_size $dx_mtv_text_size}
               {set $dx_set_ui_text_font $dx_mtv_text_font}
               {set $dx_set_ui_text_alignment $dx_mtv_text_alignment}
               {set $dx_set_ui_text_kerning $dx_mtv_text_kerning}
            )
            kDataUnhandled
         }
         ;{if $findme
         ;   {set $dx_set_ui_pos_0 {$findme get_local_pos_index 0}}
         ;   {set $dx_set_ui_pos_1 {$findme get_local_pos_index 1}}
         ;   {set $dx_set_ui_pos_2 {$findme get_local_pos_index 2}}
         ;}
      )
      #define DX_RESET_LABEL
      (
         {switch $dx_set_ui_label_tracked_ui
            (highway_overdrive_text
               {set $dx_set_ui_text_size 1}
               {set $dx_set_ui_text_font pentatonic_outline}
               {set $dx_set_ui_text_alignment kMiddleCenter}
               {set $dx_set_ui_text_kerning 0}
            )
            (highway_streak_text
               {set $dx_set_ui_text_size 1.5}
               {set $dx_set_ui_text_font pentatonic_outline}
               {set $dx_set_ui_text_alignment kMiddleLeft}
               {set $dx_set_ui_text_kerning 0}
            )
            (highway_username_text
               {set $dx_set_ui_text_size 2}
               {set $dx_set_ui_text_font pentatonic_outline}
               {set $dx_set_ui_text_alignment kMiddleCenter}
               {set $dx_set_ui_text_kerning 0}
            )
            (mtv_overlay
               {set $dx_set_ui_text_size 1}
               {set $dx_set_ui_text_font pentatonic_outline}
               {set $dx_set_ui_text_alignment kBottomRight}
               {set $dx_set_ui_text_kerning 0}
            )
         }
      )

      #define DX_APPLY_LABEL
      (
         {switch $dx_set_ui_label_tracked_ui
            (highway_overdrive_text
               {set $dx_track_overdrive_size $dx_set_ui_text_size}
               {set $dx_track_overdrive_font $dx_set_ui_text_font}
               {set $dx_track_overdrive_alignment $dx_set_ui_text_alignment}
               {set $dx_track_overdrive_kerning $dx_set_ui_text_kerning}
               {if {&& {$this in_game} {! {gamemode in_mode trainer}}}
                  {beatmatch foreach_active_player $players
                     {if {!= {$player instrument} vocals}
                        {dx_track_label_overdrive dx_track_label_overdrive $dx_track_overdrive_size $dx_track_overdrive_font $dx_track_overdrive_alignment $dx_track_overdrive_kerning {{$player get_user} get_slot_num} $dx_track_overdrive_x $dx_track_overdrive_y $dx_track_overdrive_r $dx_track_overdrive_g $dx_track_overdrive_b TRUE}
                     }
                  }
               }
            )
            (highway_streak_text
               {set $dx_track_streak_size $dx_set_ui_text_size}
               {set $dx_track_streak_font $dx_set_ui_text_font}
               {set $dx_track_streak_alignment $dx_set_ui_text_alignment}
               {set $dx_track_streak_kerning $dx_set_ui_text_kerning}
               {if {&& {$this in_game} {! {gamemode in_mode trainer}}}
                  {beatmatch foreach_active_player $player
                     {if {!= {$player instrument} vocals}
                        {dx_track_label_streak dx_track_label_streak $dx_track_streak_size $dx_track_streak_font $dx_track_streak_alignment $dx_track_streak_kerning {{$player get_user} get_slot_num} $dx_track_streak_x $dx_track_streak_y $dx_track_streak_r $dx_track_streak_g $dx_track_streak_b TRUE}
                     }
                  }
               }
            )
            (highway_username_text
               {set $dx_track_username_size $dx_set_ui_text_size}
               {set $dx_track_username_font $dx_set_ui_text_font}
               {set $dx_track_username_alignment $dx_set_ui_text_alignment}
               {set $dx_track_username_kerning $dx_set_ui_text_kerning}
               {if {&& {$this in_game} {! {gamemode in_mode trainer}}}
                  {beatmatch foreach_active_player $player
                     {if {!= {$player instrument} vocals}
                        {dx_track_label dx_track_label_username $dx_track_username_size $dx_track_username_font $dx_track_username_alignment $dx_track_username_kerning {{$player get_user} get_slot_num} $dx_track_username_x $dx_track_username_y $dx_track_username_r $dx_track_username_g $dx_track_username_b TRUE}
                        {dx_show_track_username $dx_track_username $player}
                     }
                  }
               }
            )
            (mtv_overlay
               {set $dx_mtv_text_size $dx_set_ui_text_size}
               {set $dx_mtv_text_font $dx_set_ui_text_font}
               {set $dx_mtv_text_alignment $dx_set_ui_text_alignment}
               {set $dx_mtv_text_kerning $dx_set_ui_text_kerning}
               {if {&& {$this in_game} {! {gamemode in_mode trainer}}}
                  {if $dx_mtvup
                     {{{gamemode get track_panel} find mtv_overlay} mtv_formatter} ;refresh the mtv overlay
                  }
               }
            )
            kDataUnhandled
         }
         ;{if $findme
         ;   {$findme set_local_pos $dx_set_ui_pos_0 $dx_set_ui_pos_1 $dx_set_ui_pos_2}
         ;}
      )
      (exit
         {set $dx_list_pos 0}
         {set $dx_align_array_number 0}
         {set $dx_font_array_number 0}
         {set $dx_list_item_selected FALSE}
         {set $dx_pos_remapper_array_number 3}
      )
      (enter
         {set $dx_pos_interval_list (0.01 0.1 0.5 1 5 10 50 100)}
         {set $dx_pos_current_interval {elem $dx_pos_interval_list $dx_pos_remapper_array_number}} ;1 unit
         {set $dx_align_array (kTopLeft kTopCenter kTopRight kMiddleLeft kMiddleCenter kMiddleRight kBottomLeft kBottomCenter kBottomRight)}
         {set $dx_font_array (default gangly hamilton monospace_numbers_shadow pentatonic pentatonic_bold pentatonic_boldsmall pentatonic_display pentatonic_outline pentatonic_regularcond pentatonic_regularsmall pentatonic_mononumerals rats relay_black stainless stainless_ext_blk stainless_ext_reg)}
         DX_TRANSLATE_LABEL_VARIABLES_TO_LOCAL
         {do
            ($arr {find $syscfg dx_settings $this_state view})
            ($data {array 0})
            {foreach $entry $arr
               {if {!= $entry view}
                  {push_back $data $entry}
               }
            }
            {$current_list set_data $data}
         }
      )
      #define LIST_LABEL_DIREC
      (
         {if {!= {$current_list selected_data} 0}
            {switch {$current_list selected_data}
               (1 {set $this_var dx_pos_remapper_array_number})
               (2 {set $this_var dx_set_ui_text_size})
               (3 {set $this_var dx_set_ui_text_kerning})
               (4 {set $this_var dx_align_array_number})
               (5 {set $this_var dx_font_array_number})
            }
            ;actually set our variable, based on the pressed action
            {switch $action
               ((kAction_Left kAction_Down)
                  {if_else {== $this_var dx_pos_remapper_array_number}
                     {if {> {eval {var {sprint $this_var}}} 0}
                        {set_var {sprint $this_var} {- {eval {var $this_var}} 1}}
                     }
                     {if_else {== $this_var dx_align_array_number}
                        {if {> {eval {var {sprint $this_var}}} 0}
                           {set_var {sprint $this_var} {- {eval {var $this_var}} 1}}
                        }
                        {if_else {== $this_var dx_font_array_number}
                           {if {> {eval {var {sprint $this_var}}} 0}
                              {set_var {sprint $this_var} {- {eval {var $this_var}} 1}}
                           }
                           {if {> {eval {var {sprint $this_var}}} 0}
                              {set_var {sprint $this_var} {- {eval {var $this_var}} $dx_pos_current_interval}}
                           }
                        }
                     }
                  }
               )
               ((kAction_Right kAction_Up)
                  {if_else {== $this_var dx_pos_remapper_array_number}
                     {if {< {eval {var {sprint $this_var}}} 7}
                        {set_var {sprint $this_var} {+ {eval {var $this_var}} 1}}
                     }
                     {if_else {== $this_var dx_align_array_number}
                        {if {< {eval {var {sprint $this_var}}} 8}
                           {set_var {sprint $this_var} {+ {eval {var $this_var}} 1}}
                        }
                        {if_else {== $this_var dx_font_array_number}
                           {if {< {eval {var {sprint $this_var}}} 16}
                              {set_var {sprint $this_var} {+ {eval {var $this_var}} 1}}
                           }
                           {set_var {sprint $this_var} {+ {eval {var $this_var}} $dx_pos_current_interval}}
                        }
                     }
                  }
               )
            }
            {if {== $this_var dx_pos_remapper_array_number}
               {set $dx_pos_current_interval {elem $dx_pos_interval_list $dx_pos_remapper_array_number}}
            }
            {if {== $this_var dx_align_array_number}
               {set $dx_set_ui_text_alignment {elem $dx_align_array $dx_align_array_number}}
            }
            {if {== $this_var dx_font_array_number}
               {set $dx_set_ui_text_font {elem $dx_font_array $dx_font_array_number}}
            }
            DX_APPLY_LABEL
            ;track our current list position for hiding other list elements while we have selected this one
            {set $dx_list_pos {$current_list selected_data}}
         }
      )
      (button
         (kAction_Confirm
            {if_else $dx_list_item_selected
               {set $dx_list_item_selected FALSE}
               {if_else {== {$current_list selected_data} 0} ;if we click reset
                  {do
                     DX_RESET_LABEL
                     DX_APPLY_LABEL
                  }
                  {do
                     {set $dx_list_item_selected TRUE}
                     {set $dx_list_pos {$current_list selected_data}}
                  }
               }
            }
         )
         (kAction_Cancel
            {if_else $dx_list_item_selected
               {set $dx_list_item_selected FALSE}
               {dx_state
                  {switch $dx_set_ui_label_tracked_ui
                     (highway_overdrive_text dxState_TrackHighwayOverdrive)
                     (highway_username_text dxState_TrackHighwayUsername)
                     (highway_streak_text dxState_TrackHighwayStreak)
                     (mtv_overlay dxState_MTVElements)
                  }
               }
            }
         )
         ;if we pressed Up or Down while inside a selected list item
         (kAction_Down LIST_LABEL_DIREC)
         (kAction_Up LIST_LABEL_DIREC)
         ;if we pressed left or right while hovering on a specific list item
         (kAction_Left LIST_LABEL_DIREC)
         (kAction_Right LIST_LABEL_DIREC)
      )
   )

   (dxState_TrackConfig
      (view
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 0}} os_blnk os_reset}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 1}} os_blnk {sprint "interval_" $dx_pos_remapper_array_number}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 2}} os_blnk {sprintf {localize os_trk_pitch} {int $dx_pitch}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 3}} os_blnk {sprintf {localize os_trk_fov} {int $dx_fov}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 4}} os_blnk {sprintf {localize os_trk_x} {int $dx_x_pos}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 5}} os_blnk {sprintf {localize os_trk_y} {int $dx_y_pos}}}
         {if_else {&& $dx_list_item_selected {!= $dx_list_pos 6}} os_blnk {sprintf {localize os_trk_z} {int $dx_z_pos}}}
      )
      #define DX_RESET_TRACK
      (
         {dx_passive_messenger_symbol {sprint "unimplemented"}}
      )
      (exit
         {set $dx_list_pos 0}
         {set $dx_list_item_selected FALSE}
         {set $dx_pos_remapper_array_number 3}
      )
      (enter
         {set $dx_pos_interval_list (0.01 0.1 0.5 1 5 10 50 100)}
         {set $dx_pos_current_interval {elem $dx_pos_interval_list $dx_pos_remapper_array_number}} ;1 unit
         {do
            ($arr {find $syscfg dx_settings $this_state view})
            ($data {array 0})
            {foreach $entry $arr
               {if {!= $entry view}
                  {push_back $data $entry}
               }
            }
            {$current_list set_data $data}
         }
      )
      #define LIST_TRACK_DIREC
      (
         {if {!= {$current_list selected_data} 0}
            {switch {$current_list selected_data}
               (1 {set $this_var dx_pos_remapper_array_number})
               (2 {set $this_var dx_pitch})
               (3 {set $this_var dx_fov})
               (4 {set $this_var dx_x_pos})
               (5 {set $this_var dx_y_pos})
               (6 {set $this_var dx_z_pos})
            }
            ;actually set our variable, based on the pressed action
            {switch $action
               ((kAction_Left kAction_Down)
                  {if_else {== $this_var dx_pos_remapper_array_number}
                     {if {> {eval {var {sprint $this_var}}} 0}
                        {set_var {sprint $this_var} {- {eval {var $this_var}} 1}}
                     }
                     {if_else {== $this_var dx_pitch}
                        {if {> {eval {var {sprint $this_var}}} -100}
                           {set_var {sprint $this_var} {- {eval {var $this_var}} $dx_pos_current_interval}}
                        }
                        {if_else {== $this_var dx_fov}
                           {if {> {eval {var {sprint $this_var}}} 0}
                              {set_var {sprint $this_var} {- {eval {var $this_var}} $dx_pos_current_interval}}
                           }
                           {set_var {sprint $this_var} {- {eval {var $this_var}} $dx_pos_current_interval}}
                        }
                     }
                  }
               )
               ((kAction_Right kAction_Up)
                  {if_else {== $this_var dx_pos_remapper_array_number}
                     {if {< {eval {var {sprint $this_var}}} 7}
                        {set_var {sprint $this_var} {+ {eval {var $this_var}} 1}}
                     }
                     {if_else {== $this_var dx_pitch}
                        {if {< {eval {var {sprint $this_var}}} 100}
                           {set_var {sprint $this_var} {+ {eval {var $this_var}} $dx_pos_current_interval}}
                        }
                        {if_else {== $this_var dx_fov}
                           {if {< {eval {var {sprint $this_var}}} 100}
                              {set_var {sprint $this_var} {+ {eval {var $this_var}} $dx_pos_current_interval}}
                           }
                           {set_var {sprint $this_var} {+ {eval {var $this_var}} $dx_pos_current_interval}}
                        }
                     }
                  }
               )
            }
            {set $dx_track_config_changed TRUE}
            {dx_track_configurer}
            ;track our current list position for hiding other list elements while we have selected this one
            {set $dx_list_pos {$current_list selected_data}}
         }
      )
      (button
         (kAction_Confirm
            {if_else $dx_list_item_selected
               {set $dx_list_item_selected FALSE}
               {if_else {== {$current_list selected_data} 0} ;if we click reset
                  {do
                     DX_RESET_TRACK
                     {set $dx_track_config_changed TRUE}
                     {dx_track_configurer}
                  }
                  {do
                     {set $dx_list_item_selected TRUE}
                     {set $dx_list_pos {$current_list selected_data}}
                  }
               }
            }
         )
         (kAction_Cancel
            {if_else $dx_list_item_selected
               {set $dx_list_item_selected FALSE}
               {dx_state dxState_HighwayOther}
            }
         )
         ;if we pressed Up or Down while inside a selected list item
         (kAction_Down LIST_TRACK_DIREC)
         (kAction_Up LIST_TRACK_DIREC)
         ;if we pressed left or right while hovering on a specific list item
         (kAction_Left LIST_TRACK_DIREC)
         (kAction_Right LIST_TRACK_DIREC)
      )
   )